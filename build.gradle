group 'uk.ac.ebi.subs'
version '0.3.4-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.ssh'

mainClassName = "uk.ac.ebi.subs.SamplesAgentApplication"

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
}

dependencies {
    compile("uk.ac.ebi.subs:subs-processing-model:0.2.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-data-model:0.3.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-messaging:0.4.0-SNAPSHOT")

    optional("org.springframework.boot:spring-boot-configuration-processor")

    compile ('uk.ac.ebi.biosamples:biosamples-spring-boot-starter:4.0.0-SNAPSHOT') {
        changing = true
        exclude group: 'org.springframework.boot'
    }

    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.hamcrest'
    }
    testCompile("org.hamcrest:java-hamcrest:2.0.0.0")
}

task sourceJar(type: Jar) { from sourceSets.main.allJava }

artifacts {
    archives jar
    archives sourceJar {
        classifier "sources" 
        destinationDir = new File("build/libs/sources")
    }
}

task('externalCiTest', type: Test) {
	useJUnit {
		excludeCategories 'uk.ac.ebi.subs.agent.utils.BioSamplesDependentTest'
	}
	testLogging {
		exceptionFormat = 'full'
	}
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

            }

            pom.project {
                packaging 'jar'
                url 'https://github.com/EMBL-EBI-SUBS/subs-samples-agent'

                scm {
                    url 'https://github.com/EMBL-EBI-SUBS/subs-samples-agent.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'subs-internal'
                        name 'EMBL-EBI-SUBS'
                        email 'subs-internal@ebi.ac.uk'
                    }
                }
            }
        }
    }
}

compileJava.dependsOn(processResources)

springBoot {
    executable = true
}

ssh.settings {
    user = 'sub_adm'
}

if (project.hasProperty('sshKeyFile')) {
    ssh.settings.identity = new File(sshKeyFile)
}

if (project.hasProperty('sshPassphrase')) {
    ssh.settings.passphrase = sshPassphrase
}

task printJarName {
    doLast {
        println "$jar.archivePath.name"
    }

}

task printJarFullPath {
    doLast {
        println "$jar.archivePath.absolutePath"
    }
}

processTestResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

task printClasspath doLast {
    configurations.compile.each { println it }
}

ext.gradle_env = hasProperty('env') ? env : 'dev'

def devDeployHostName = hasProperty(project.name + "DevDeployTarget") ? getProperty(project.name + "DevDeployTarget") : 'localhost'
def testDeployHostName = hasProperty(project.name + "TestDeployTarget") ? getProperty(project.name + "TestDeployTarget") : 'localhost'
def prodDeployHostName = hasProperty(project.name + "ProdDeployTarget") ? getProperty(project.name + "ProdDeployTarget") : 'localhost'

remotes {
    submission_dev {
        role('dev')
        host = devDeployHostName
    }
    submission_test {
        role('test')
        host = testDeployHostName
    }
    submission_prod {
        role('prod')
        host = prodDeployHostName
    }
}

task deployJar(type: SshTask, dependsOn: 'assemble') {
    doLast {
        File jarDeployDir = new File("/data/$gradle_env/$project.name/jar")
        sshRun(jarDeployDir)
    }
    ssh.settings {
        knownHosts = allowAnyHosts
    }
}

private Object sshRun(File jarDeployDir) {
    ssh.run {
        session(remotes.role(gradle_env)) {
            execute "/homes/sub_adm/create_dirs.sh $gradle_env $project.name", ignoreError: false
            println "Uploading jar $jar.archivePath.name to $jarDeployDir area on $remote"
            put(jar.archivePath.absolutePath, jarDeployDir.absolutePath)
            println "creating symbolic link to $jar.archiveName"
            def result = execute "ln -s -f $jarDeployDir/$jar.archiveName $jarDeployDir/$jar.baseName" + ".jar", ignoreError: true
            println result
            println "updating permissions of $jarDeployDir/$jar.archiveName"
            execute "chmod u+x $jarDeployDir/*", ignoreError: false
        }
    }
}

